// json-filter

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    alias(libs.plugins.versions)

    // to ensure clean code
    alias(libs.plugins.errorprone)

    // for deployment to Maven Central
    alias(libs.plugins.maven.publish)
}

group   = 'com.arakelian'
version = '5.0.0'


apply from: "core.gradle"

wrapper {
    gradleVersion = '9.3.1'
}

mavenPublishing {
    coordinates(project.group.toString(), project.name, project.version.toString())

    pom {
        name = "Json Filter"
        description = "High-speed stream-based filtering of arbitrarily large JSON documents."
        url = "https://github.com/arakelian/json-filter"

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'arakelian'
                name = 'Greg Arakelian'
                email = 'greg@arakelian.com'
            }
        }

        scm {
            connection = 'scm:git:https://github.com/arakelian/json-filter.git'
            developerConnection = 'scm:git:git@github.com:arakelian/json-filter.git'
            url = 'https://github.com/arakelian/json-filter.git'
        }
    }
}

dependencies {
    annotationProcessor libs.immutables.value

    // annotations
    api libs.immutables.annotations

    // for date utils
    api libs.more.commons

    // configure errorprone version
    errorprone libs.errorprone.core

    // we use Guava directly
    api libs.guava

    // logging (Log4j versions managed by BOM)
    testImplementation platform(libs.log4j.bom)
    testImplementation libs.log4j.api
    testImplementation libs.log4j.core
    testImplementation libs.log4j.slf4j2.impl
    testImplementation libs.jcl.over.slf4j
    testImplementation libs.jul.to.slf4j
    api                libs.slf4j.api

    // for unit testing (JUnit versions managed by BOM)
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
}
